pkgname=plasma-share-uploader-git
pkgver=0.1.0
pkgrel=1
pkgdesc='Create custom upload targets for the KDE Plasma 6 Share menu'
arch=('x86_64')
url='https://github.com/ostrich/plasma-share-uploader'
license=('GPL3')
depends=('qt6-base' 'purpose' 'kcoreaddons' 'knotifications')
makedepends=('cmake' 'python')
source=('git+https://github.com/ostrich/plasma-share-uploader.git')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/plasma-share-uploader"
  git describe --long --tags --always | sed 's/^v//;s/-/./g'
}

build() {
  cmake -S "$srcdir/plasma-share-uploader" -B "$srcdir/build" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build "$srcdir/build"
}

package() {
  DESTDIR="$pkgdir" cmake --install "$srcdir/build"
}
